---
group: "nix-and-home-manager-setup"
title: "Nix와 home-manager 설치"
description: "Nix와 home-manager 설치 및 설정"
pubDate: "2023-04-02"
heroImage: "https://repository-images.githubusercontent.com/3386088/c9347c80-7b49-11e9-972e-03413da7be7a"
tags: ["nix", "home-manager", "flake"]
lang: ko
draft: false
---

## 목표

이 게시물에서는 어떤 개발 환경이든 사용할 수 있는 Nix와 Home Manager 설정을 설정할 것입니다.
최종적으로 다음과 같은 목표를 달성하고자 합니다:

1. 다른 컴으로 쉽게 이전할 수 있는 Nix와 Home Manager 설정 설정.
2. homebrew 또는 apt 사용의 99%를 Home Manager로 대체.

조급한 분들을 위해 전체 코드 스니펫은 https://github.com/rexk/dotfiles/tree/nix-flake-install-stage-01에서 확인하실 수 있습니다.


## Nix 인스톨

https://nixos.org/download.html#download-nix

각 설치 옵션에 대해서는 자세히 설명하지 않겠습니다. 저는 권장 설치 지침을 선택했습니다.

## dotfile git 저장소 설정

설치가 (다소) 일반화되도록 하려면 서로 다른 기계 간에 설정을 공유하는 방법이 필요합니다.
이를 위해 git으로 도트파일을 추적 및 공유할 것입니다.

이를 달성하는 다양한 기사가 있습니다. 제 설정은 특히 두 가지 기사에서 영감을 받았습니다:

https://drewdevault.com/2019/12/30/dotfiles.html
https://www.atlassian.com/git/tutorials/dotfiles
요약하면, 저희 도트파일 git 저장소는 다음과 같은 특징을 가집니다:

1. 모든 파일은 .gitignore로 민감한 정보를 실수로 커밋하는 것을 방지하기 위해 무시됩니다.
2. Git 디렉토리는 작업 트리와 다른 위치에 배치됩니다. 이는 nix의 flake가 gitignore 파일을 존중하지 않도록 합니다. 이 도트파일 저장소를 설정하는 자세한 지침은 다른 게시물에서 다룰 것입니다.

지금은 $HOME/.config 디렉토리에서 작업할 것으로 생각해주세요.

아래 파일을 $HOME/.config 디렉토리에 추가하기 전에 작성해 주세요.

```sh
cat <<EOF>$HOME/.config/.gitignore
*
!.gitignore

EOF
```

## nix-darwin 설치

현재 맥을 사용하고 계시면, nix-darwin도 같이 설치하면 좋기는 합니다.
다만 필수 요소는 아니라서, 다른 포스트에서 nix-darwin과 flake설정을 다뤄 볼까 합니다. 일단은
홈 매니저에 집중을 해봅시다.


## Home Manager를 flake기반으로 설치

Home Manager의 공식 설치 문서를 따라하면, 아래와 같은 flake.nix파일을 생성하게 됩니다.

```nix
# flake.nix
{
  description = "Home Manager configuration of Jane Doe";

  inputs = {
    # Specify the source of Home Manager and Nixpkgs.
    nixpkgs.url = "github:nixos/nixpkgs/nixos-22.11";
    home-manager = {
      url = "github:nix-community/home-manager/release-22.11";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = { nixpkgs, home-manager, ... }:
    let
      system = "x86_64-linux";
      pkgs = nixpkgs.legacyPackages.${system};
    in {
      homeConfigurations.jdoe = home-manager.lib.homeManagerConfiguration {
        inherit pkgs;

        # Specify your home configuration modules here, for example,
        # the path to your home.nix.
        modules = [
          ./home.nix
        ];

        # Optionally use extraSpecialArgs
        # to pass through arguments to home.nix
      };
    };
}

# home.nix
{
  # 아래 필드들을 본인 컴 상황에 맡게 설정해야 합니다.
  home.username = "jdoe";
  home.homeDirectory = "/home/jdoe";
  home.stateVersion = "22.11";
}
```

설치 지침은 home.nix와 flake.nix에서 사용자별 정보를 업데이트하도록 지시하지만,
제 사용 사례에는 충분하지 않습니다. 제 각 개발 환경에서 동일한 flake 파일을 사용할 수 있어야합니다.
그러나 저는 각 개발 환경마다 다른 사용자 이름을 가지고 있습니다.

나의 각 컴에서 flake.nix와 home.nix를 사용하려면, 사용자별 정보를 flake.nix와 home.nix에서 추상화해야합니다.

그래서 다음과 같은 변경을 만들었습니다.


```diff
 {
-  description = "Home Manager configuration of Jane Doe";
+  description = "Home Manager configuration";
 
   inputs = {
     # Specify the source of Home Manager and Nixpkgs.
-    nixpkgs.url = "github:nixos/nixpkgs/nixos-22.11";
+    nixpkgs.url = "github:NixOS/nixpkgs/nixos-22.11";
+    nixpkgs-unstable.url = "github:NixOs/nixpkgs/nixos-unstable";
     home-manager = {
       url = "github:nix-community/home-manager/release-22.11";
       inputs.nixpkgs.follows = "nixpkgs";
     };
+    flake-utils.url = "github:numtide/flake-utils";
   };
 
-  outputs = { nixpkgs, home-manager, ... }:
+  outputs = { nixpkgs, flake-utils, home-manager, nixpkgs-unstable, ... }:
     let
-      system = "x86_64-linux";
-      pkgs = nixpkgs.legacyPackages.${system};
-    in {
-      homeConfigurations.jdoe = home-manager.lib.homeManagerConfiguration {
-        inherit pkgs;
+      utils = flake-utils;
+      user = import ./user.nix;
+    in
+    utils.lib.eachDefaultSystem (system:
+      let
+        stable-pkgs = nixpkgs.legacyPackages.${system};
+        unstable-pkgs = nixpkgs-unstable.legacyPackages.${system};
+        pkgs = stable-pkgs // {
+          # provides alias for all ustable pkgs
+          unstable = unstable-pkgs;
 
-        # Specify your home configuration modules here, for example,
-        # the path to your home.nix.
-        modules = [
-          ./home.nix
-        ];
+          starship = unstable-pkgs.starship;
+          git = unstable-pkgs.git;
+        };
+      in
+      {
+        formatter = pkgs.nixpkgs-fmt;
 
-        # Optionally use extraSpecialArgs
-        # to pass through arguments to home.nix
-      };
-    };
+        packages = {
+          homeConfigurations.${user.name} = home-manager.lib.homeManagerConfiguration {
+            inherit pkgs;
+
+            # Specify your home configuration modules here, for example,
+            # the path to your home.nix.
+            modules = [
+              ./home.nix
+            ];
+
+            # Optionally use extraSpecialArgs
+            # to pass through arguments to home.nix
+            extraSpecialArgs = {
+              inherit pkgs;
+            };
+          };
+        };
+      });
 }
```

```diff
+let
+  user = import ./user.nix;
+ in
 {
-  home.username = "jdoe";
-  home.homeDirectory = "/home/jdoe";
+  home.username = user.name;
+  home.homeDirectory = user.homeDir;
   home.stateVersion = "22.11";
 }

```

`user.nix` 파일은 `user.nix.tpl` 파일로 부터 렌더링을 합니다.

```nix
# user.nix.tpl
{
  name = "$USER";
  hostname = "$(hostname)";
  homeDir = "$HOME";
}
```

위 파일은 아래 쉘 스크립트를 통해서 렌더링을 하게 됩니다.

```sh
#!/usr/bin/env sh

#-------------------------------------------------
# render-user-nix
#-------------------------------------------------
# render user specific nix files for home-manager
# If the current environment is darwin, 
# we will also render configurations for nix-darwin (osx)

render_user_nix() {
  local file="$1"
  local dest="$2"
  if [ -f "$file" ];
  then
    local tmp="/tmp/$(basename $1)"
    # substitutes sh experssions
   (echo "cat <<EOF"; cat $file; echo EOF) | sh > $tmp
    cp $tmp $dest
  fi
}

CONFIG_DIR=${XDG_CONFIG_HOME:-$HOME/.config}

render_user_nix $CONFIG_DIR/home-manager/user.nix.tpl $CONFIG_DIR/home-manager/user.nix

case "$(uname -s)" in
  Darwin)
    render_user_nix $CONFIG_DIR/nix-darwin/user.nix.tpl $CONFIG_DIR/nix-darwin/user.nix
    ;;
esac

# no need to keep the function
unset render_user_nix
```
다른 기계에서도 이 스크립트가 필요하기 때문에, 이 스크립트를 ~/.config/bin 폴더에 위치시킬 것입니다.

또한, https://github.com/numtide/flake-utils를 내 flake에 가져왔습니다. 이를 통해 각각의 사용하는 시스템마다 flake 설정을 정의할 수 있습니다.

## home manager 설정 적용 

Since we are using flake installation, usual `home-manager` command is not available in our path.
We need to explicitly run `nix run` command.

If my user name is jdoe, the command will look like the following:


```sh
nix run /home/jdoe/home-manager#homeConfigurations.jdoe.activationPackage
```

그러나 이러한 설정을 모든 기계에서 작동하도록 만들고 싶으므로, home-manager 활성화를 위한
다른 스크립트를 제공해 봅시다.


```sh
#!/usr/bin/env sh

set -e;

#-------------------------------------------------
# hms
#-------------------------------------------------
# aliasing "home-manager switch" command for flake
CONFIG_DIR=${XDG_CONFIG_HOME:=$HOME/.config}
/nix/var/nix/profiles/default/bin/nix \
  --extra-experimental-features "nix-command flakes" \
  run $CONFIG_DIR/home-manager#homeConfigurations.\"$USER\".activationPackage
```


## 최종 파일 트리

```sh
├── bin
│  ├── render-user-nix
│  └── hms 
└── home-manager
   ├── flake.nix
   ├── flake.lock
   ├── home.nix
   ├── user.nix <-- generated by render-user-nix. Not checked in
   └── user.nix.tpl
```

## 참조

* Nix 설치: https://nixos.org/download.html
* Home Manager 매뉴얼: https://rycee.gitlab.io/home-manager/
* https://drewdevault.com/2019/12/30/dotfiles.html
* https://www.atlassian.com/git/tutorials/dotfiles